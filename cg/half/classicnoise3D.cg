//
// GLSL textureless classic 3D noise "cnoise",
// with an RSL-style periodic variant "pnoise".
// Author:  Stefan Gustavson (stefan.gustavson@liu.se)
// Version: 2011-10-11
//
// Many thanks to Ian McEwan of Ashima Arts for the
// ideas for permutation and gradient selection.
//
// Copyright (c) 2011 Stefan Gustavson. All rights reserved.
// Distributed under the MIT license. See LICENSE file.
// https://github.com/ashima/webgl-noise
//

half3 mod289(half3 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

half4 mod289(half4 x)
{
  return x - floor(x * (1.0 / 289.0)) * 289.0;
}

half4 permute(half4 x)
{
  return mod289(((x*34.0)+1.0)*x);
}

half4 taylorInvSqrt(half4 r)
{
  return 1.79284291400159 - 0.85373472095314 * r;
}

half3 fade(half3 t) {
  return t*t*t*(t*(t*6.0-15.0)+10.0);
}

// Classic Perlin noise
half cnoise(half3 P)
{
  half3 Pi0 = floor(P); // Integer part for indexing
  half3 Pi1 = Pi0 + half3(1.0); // Integer part + 1
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  half3 Pf0 = frac(P); // Fractional part for interpolation
  half3 Pf1 = Pf0 - half3(1.0); // Fractional part - 1.0
  half4 ix = half4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  half4 iy = half4(Pi0.yy, Pi1.yy);
  half4 iz0 = Pi0.zzzz;
  half4 iz1 = Pi1.zzzz;

  half4 ixy = permute(permute(ix) + iy);
  half4 ixy0 = permute(ixy + iz0);
  half4 ixy1 = permute(ixy + iz1);

  half4 gx0 = ixy0 * (1.0 / 7.0);
  half4 gy0 = frac(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = frac(gx0);
  half4 gz0 = half4(0.5) - abs(gx0) - abs(gy0);
  half4 sz0 = step(gz0, half4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  half4 gx1 = ixy1 * (1.0 / 7.0);
  half4 gy1 = frac(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = frac(gx1);
  half4 gz1 = half4(0.5) - abs(gx1) - abs(gy1);
  half4 sz1 = step(gz1, half4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  half3 g000 = half3(gx0.x,gy0.x,gz0.x);
  half3 g100 = half3(gx0.y,gy0.y,gz0.y);
  half3 g010 = half3(gx0.z,gy0.z,gz0.z);
  half3 g110 = half3(gx0.w,gy0.w,gz0.w);
  half3 g001 = half3(gx1.x,gy1.x,gz1.x);
  half3 g101 = half3(gx1.y,gy1.y,gz1.y);
  half3 g011 = half3(gx1.z,gy1.z,gz1.z);
  half3 g111 = half3(gx1.w,gy1.w,gz1.w);

  half4 norm0 = taylorInvSqrt(half4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  half4 norm1 = taylorInvSqrt(half4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  half n000 = dot(g000, Pf0);
  half n100 = dot(g100, half3(Pf1.x, Pf0.yz));
  half n010 = dot(g010, half3(Pf0.x, Pf1.y, Pf0.z));
  half n110 = dot(g110, half3(Pf1.xy, Pf0.z));
  half n001 = dot(g001, half3(Pf0.xy, Pf1.z));
  half n101 = dot(g101, half3(Pf1.x, Pf0.y, Pf1.z));
  half n011 = dot(g011, half3(Pf0.x, Pf1.yz));
  half n111 = dot(g111, Pf1);

  half3 fade_xyz = fade(Pf0);
  half4 n_z = lerp(half4(n000, n100, n010, n110), half4(n001, n101, n011, n111), fade_xyz.z);
  half2 n_yz = lerp(n_z.xy, n_z.zw, fade_xyz.y);
  half n_xyz = lerp(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}

// Classic Perlin noise, periodic variant
half pnoise(half3 P, half3 rep)
{
  half3 Pi0 = fmod(floor(P), rep); // Integer part, modulo period
  half3 Pi1 = fmod(Pi0 + half3(1.0), rep); // Integer part + 1, mod period
  Pi0 = mod289(Pi0);
  Pi1 = mod289(Pi1);
  half3 Pf0 = frac(P); // Fractional part for interpolation
  half3 Pf1 = Pf0 - half3(1.0); // Fractional part - 1.0
  half4 ix = half4(Pi0.x, Pi1.x, Pi0.x, Pi1.x);
  half4 iy = half4(Pi0.yy, Pi1.yy);
  half4 iz0 = Pi0.zzzz;
  half4 iz1 = Pi1.zzzz;

  half4 ixy = permute(permute(ix) + iy);
  half4 ixy0 = permute(ixy + iz0);
  half4 ixy1 = permute(ixy + iz1);

  half4 gx0 = ixy0 * (1.0 / 7.0);
  half4 gy0 = frac(floor(gx0) * (1.0 / 7.0)) - 0.5;
  gx0 = frac(gx0);
  half4 gz0 = half4(0.5) - abs(gx0) - abs(gy0);
  half4 sz0 = step(gz0, half4(0.0));
  gx0 -= sz0 * (step(0.0, gx0) - 0.5);
  gy0 -= sz0 * (step(0.0, gy0) - 0.5);

  half4 gx1 = ixy1 * (1.0 / 7.0);
  half4 gy1 = frac(floor(gx1) * (1.0 / 7.0)) - 0.5;
  gx1 = frac(gx1);
  half4 gz1 = half4(0.5) - abs(gx1) - abs(gy1);
  half4 sz1 = step(gz1, half4(0.0));
  gx1 -= sz1 * (step(0.0, gx1) - 0.5);
  gy1 -= sz1 * (step(0.0, gy1) - 0.5);

  half3 g000 = half3(gx0.x,gy0.x,gz0.x);
  half3 g100 = half3(gx0.y,gy0.y,gz0.y);
  half3 g010 = half3(gx0.z,gy0.z,gz0.z);
  half3 g110 = half3(gx0.w,gy0.w,gz0.w);
  half3 g001 = half3(gx1.x,gy1.x,gz1.x);
  half3 g101 = half3(gx1.y,gy1.y,gz1.y);
  half3 g011 = half3(gx1.z,gy1.z,gz1.z);
  half3 g111 = half3(gx1.w,gy1.w,gz1.w);

  half4 norm0 = taylorInvSqrt(half4(dot(g000, g000), dot(g010, g010), dot(g100, g100), dot(g110, g110)));
  g000 *= norm0.x;
  g010 *= norm0.y;
  g100 *= norm0.z;
  g110 *= norm0.w;
  half4 norm1 = taylorInvSqrt(half4(dot(g001, g001), dot(g011, g011), dot(g101, g101), dot(g111, g111)));
  g001 *= norm1.x;
  g011 *= norm1.y;
  g101 *= norm1.z;
  g111 *= norm1.w;

  half n000 = dot(g000, Pf0);
  half n100 = dot(g100, half3(Pf1.x, Pf0.yz));
  half n010 = dot(g010, half3(Pf0.x, Pf1.y, Pf0.z));
  half n110 = dot(g110, half3(Pf1.xy, Pf0.z));
  half n001 = dot(g001, half3(Pf0.xy, Pf1.z));
  half n101 = dot(g101, half3(Pf1.x, Pf0.y, Pf1.z));
  half n011 = dot(g011, half3(Pf0.x, Pf1.yz));
  half n111 = dot(g111, Pf1);

  half3 fade_xyz = fade(Pf0);
  half4 n_z = lerp(half4(n000, n100, n010, n110), half4(n001, n101, n011, n111), fade_xyz.z);
  half2 n_yz = lerp(n_z.xy, n_z.zw, fade_xyz.y);
  half n_xyz = lerp(n_yz.x, n_yz.y, fade_xyz.x); 
  return 2.2 * n_xyz;
}
